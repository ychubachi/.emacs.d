#+STARTUP: show2levels

これだけは絶対に必要！と思える設定。

* パッケージ
** leafの追加パッケージ

leafに追加するパッケージです。

#+begin_src emacs-lisp
  (leaf *leaf-optional-packages
    :config
    (leaf leaf-convert
      :straight t)
    (leaf leaf-tree
      :straight t
      :custom ((imenu-list-size . 30)
               (imenu-list-position . 'left)))
    (leaf hydra
      :straight t)
;;    (leaf el-get
;;      :straight t
;;      :custom ((el-get-git-shallow-clone  . t)))
    (leaf diminish
      :straight t))
#+end_src

* キーボードバインディング
** C-hをBSにする[C-h]

- [[https://www.emacswiki.org/emacs/BackspaceKey][[Home] Backspace Key]]
  - 記述が間違っている
- [[http://malkalech.com/emacs_c-h_backspace][EmacsのC-hでBackspaceするのに紆余曲折したことのメモ | Jenemal Notes]]

#+begin_src emacs-lisp
  (define-key key-translation-map [?\C-h] [?\C-?])
#+end_src

#+RESULTS:
: [127]

- 次の方法だとpereditでうまくいかない

#+begin_example
  (leaf *backspace
    :init (global-set-key (kbd "C-^") help-map)
    :bind (("C-h" . delete-backward-char)))
#+end_src

** C-^をヘルプにする[C-^]

#+begin_src emacs-lisp
  (global-set-key (kbd "C-^") help-map)
#+end_src

** UndoをC-zにする[C-z]
- 指が覚えてしまっている

#+begin_src emacs-lisp
  (leaf *undo :bind (("C-z" . undo)))
#+end_src

#+RESULTS:
: *undo

* 日本語関係
** Linuxでmozcを使った日本語入力の設定（2022年版）                 :PBLISHED:
:PROPERTIES:
:BLOG:     plover
:DATE:     [2021-11-23 22:10:47]
:OPTIONS:  toc:nil num:nil todo:nil pri:nil tags:nil ^:nil
:CATEGORY: Tech
:POST_TAGS: Emacs, Lisp, Mozc, IME
:ID:       o2b:16b90138-c679-4ad8-b1f2-fab55daf0058
:POST_DATE: [2021-11-23 Tue 22:11]
:POSTID:   463
:END:

#+begin_comment
+ Mozcのローマ字配列の変更方法
  + mozc_toolを利用する
  + /usr/lib/mozc/mozc_tool --mode=config_dialog
#+end_comment

*** 説明
日本語入力周りの設定を見直してみました。

- 参考
  - [[https://w.atwiki.jp/ntemacs/pages/48.html][emacs-mozc を動かすための設定（Emacs 設定編） - NTEmacs @ ウィキ - atwiki（アットウィキ）]]

*** 全体の設定
Mozcに関連する設定の全体です。個別の説明は[[*Mozcサーバと通信するためのヘルパーコマンド][こちら]]から。

#+begin_src emacs-lisp :noweb yes
  (leaf mozc
    ;; :if (eq system-type 'gnu/linux)
    :straight t
    :config
    <<mozc-helper>>
    <<mozc-im>>
    <<mozc-im-state-isearch>>
    <<mozc-cursor-color>>
    <<mozc-posframe>>
    <<mozc-windows-advice>>)
#+end_src

#+RESULTS:
: mozc

*** Mozcサーバと通信するためのヘルパーコマンドの設定
Mozcとのやり取りをするためのヘルパーを設定します。
WSL環境では、mozc_emacs_helper.exeに置き換えると、Windows側のGoogle IMEが使えます（後述のWindows用設定が必要）。

+ [[https://w.atwiki.jp/ntemacs/pages/50.html][emacs-mozc を動かすための設定（mozc_emacs_helper コンパイル編） - NTEmacs @ ウィキ - atwiki（アットウィキ）]]
+ [[https://w.atwiki.jp/ntemacs/pages/61.html][emacs-mozc を動かすための設定（WSL 設定編） - NTEmacs @ ウィキ - atwiki（アットウィキ）]]

#+NAME: mozc-helper
#+begin_src emacs-lisp :tangle no
  (cond ((eq system-type 'windows-nt)
      (setq mozc-helper-program-name "~/Dropbox/bin/mozc_emacs_helper.exe"))
      (t (setq mozc-helper-program-name "mozc_emacs_helper")))
  ;; (if (getenv "WSLENV")
  ;;     ;; (setq mozc-helper-program-name "mozc_emacs_helper_win.sh")
  ;;     (setq mozc-helper-program-name "mozc_emacs_helper")
  ;;   (setq mozc-helper-program-name "mozc_emacs_helper"))
#+end_src

#+RESULTS: mozc-helper
: ~/Dropbox/bin/mozc_emacs_helper.exe

*** Mozcによる変換操作の設定[C-o][C-j]
標準の入力方式をMozcに設定します。
C-oで変換モード、C-jで直接入力モードに切り替えます。

なお、org-modeでC-jを（特殊な？）改行コマンドとして使用しているので、leafのbind*でオーバライドします（便利！）。ちなみに、org-modeでの改行はC-mかRETでもできます。C-jの出番がどこにあるのか、わかりません。

デフォルトでは、IMの状態をトグルするメソッドしかないので、それぞれの状態に遷移するメソッドを用意します。

IMEがoffのとき、isearchに入り、IMEをonにして検索したあと、戻ったときにIMEがoffになって欲しいので、そのための設定をします（migemoを使うのなら、あまり必要ない設定かも）。

#+NAME: mozc-im
#+begin_src emacs-lisp :tangle no :noweb yes
  (leaf mozc-im
    :straight t
    :require t
    :custom ((default-input-method . "japanese-mozc-im"))
    :bind* (("C-o" . enable-input-method)
            ("C-j" . disable-input-method))
    :config
    (defun enable-input-method (&optional arg interactive)
      (interactive "P\np")
      (if (not current-input-method)
          (toggle-input-method arg interactive)))

    (defun disable-input-method (&optional arg interactive)
      (interactive "P\np")
      (if current-input-method
          (toggle-input-method arg interactive)))

    (defvar-local mozc-im-mode nil)

    (add-hook 'mozc-im-activate-hook
              (lambda nil
                (setq mozc-im-mode t)))

    (add-hook 'mozc-im-deactivate-hook
              (lambda nil
                (setq mozc-im-mode nil)))

    (defvar-local im-state nil)

    (add-hook 'isearch-mode-hook
              (lambda () (setq im-state mozc-im-mode)))

    (add-hook 'isearch-mode-end-hook
              (lambda ()
                (unless (eq im-state mozc-im-mode)
                  (if im-state
                      (activate-input-method default-input-method)
                    (deactivate-input-method))))))
#+end_src

#+RESULTS: mozc-im
: mozc-im

*** カーソルカラーを設定する
現在の状態に合わせて、カーソルの色を設定します。この設定では、直接入力の時はグレイ、変換モードのときは緑にしています。リードオンリーでは黄色になります。それ以外の状態は、私は使いませんが念の為。

なお、Emacsで使えるカラーの一覧は、M-x list-colors-displayで確認できます。

#+NAME: mozc-cursor-color
#+begin_src emacs-lisp :tangle no
  (leaf mozc-cursor-color
    :straight (mozc-cursor-color :type git :host github
                                 :repo "iRi-E/mozc-el-extensions")
    :require t
    :config
    (setq mozc-cursor-color-alist
          '((direct        . "gray")
            (read-only     . "yellow")
            (hiragana      . "green")
            (full-katakana . "goldenrod")
            (half-ascii    . "dark orchid")
            (full-ascii    . "orchid")
            (half-katakana . "dark goldenrod")))
    (advice-add 'mozc-cursor-color-update :around
                (lambda (orig-fun &rest args)
                  (let ((mozc-mode mozc-im-mode))
                    (apply orig-fun args)))))
#+end_src

#+RESULTS: mozc-cursor-color
: mozc-cursor-color

*** 変換候補をposframeで表示する

posframeはEmacs26から追加された機能です。Emacs26は3年ほど前にリリースされたようですから、私は長いことposframeを知らなかったことになります。

今回の設定変更の目玉商品です。「[[https://blog.deltabox.site/post/2019/06/mozc-posframe/][mozcの候補をposframeで表示するEmacs拡張を作った]]」に書いている通り、org-modeでの変換の際の表示崩れは目に余るものがありました。

作者さん、ありがとうございます。

#+NAME: mozc-posframe
#+begin_src emacs-lisp :tangle no
  (leaf mozc-posframe
    :straight (mozc-posframe :type git :host github :repo "derui/mozc-posframe")
    :config
    (mozc-posframe-register)
    (setq mozc-candidate-style 'posframe))
#+end_src

#+RESULTS: mozc-posframe
: mozc-posframe

*** Windows用adviceの設定

- https://w.atwiki.jp/ntemacs/pages/48.html
  - 5) （「mozc_emacs_helper コンパイル編」利用の場合）init.el 等に以下の elisp の設定を追加する。

#+NAME: mozc-windows-advice
#+begin_src emacs-lisp :tangle no
  (leaf *mozc-win
    :if (eq system-type 'windows-nt)
    :config
    (advice-add 'mozc-session-execute-command
	    :after (lambda (&rest args)
		     (when (eq (nth 0 args) 'CreateSession)
		       (mozc-session-sendkey '(Hankaku/Zenkaku))))))
#+end_src

*** COMMENT メモ

#+begin_comment
- [[https://qiita.com/ignorant/items/1c4f729f9147fb878f10][WSL の Emacs で日本語入力 - Qiita]]
  - Windows側のGoogle IMEを利用する
  - aptでインストールされたと思われるmozc_emacs_helper（バイナリ）を使ってみる。
    - 既に設定されていた
      [[https://github.com/smzht/mozc_emacs_helper][smzht/mozc_emacs_helper: mozc_emacs_helper for Windows]]
      これはUnix用

- Windows用のmozc_emacs_helper.exeを利用する
  - [[https://github.com/smzht/mozc_emacs_helper][smzht/mozc_emacs_helper: mozc_emacs_helper for Windows]]
    ここからmozc_emacs_helper.exeをダウンロードして
    ~/Dropbox/bin/以下においておく

  - ~/bin/にmozc_emacs_helper_win.shを作成する

    #+begin_src bash :tangle no
      #!/bin/sh

      ~/Dropbox/bin/mozc_emacs_helper.exe "$@" 2> /dev/null
    #+end_src
#+end_comment

#+begin_comment
IME ON  -> isearch -> OFF -> done -> ON
IME ON  -> isearch -> ON  -> done -> ON
IME OFF -> isearch -> ON  -> done -> ON*
IME OFF -> isearch -> OFF -> done -> OFF
*のところで状態が狂う。
#+end_comment

** フォントの設定（Windows）

|mmmmmmmmmm|
|llllllllll|
|あいうえお|
|1,2,3,4,5|

#+begin_src emacs-lisp
  (when (eq system-type 'windows-nt)
    ;; 通常使用するフォント
    (set-frame-font "PlemolJP-12" nil t)
    ;; 行間
    (setq-default line-spacing 0)
    ;; IME未確定時のフォント設定
    (modify-all-frames-parameters '((ime-font . "PlemolJP-12"))))
#+end_src
